apply plugin: "java"
apply plugin: 'maven-publish'

version '1.0'

sourceSets.main.java.srcDirs = ["src"]

group = 'com.github.Agzam4'

/*
* For Gradle 9.0 we need use sourceCompatibility inside java {...}
*/
java {
    sourceCompatibility = JavaVersion.VERSION_17
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

repositories{
    mavenCentral()
    maven{ url 'https://jitpack.io' }
}

ext{
    mindustryVersion = 'v149'
}

dependencies{
    compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
    compileOnly "com.github.Anuken.mindustryjitpack:core:$mindustryVersion"
    compileOnly "org.telegram:telegrambots:6.9.7.1"
    compileOnly 'org.xerial:sqlite-jdbc:3.49.1.0'
}

configurations.all{
    resolutionStrategy.eachDependency { details ->
        if(details.requested.group == 'com.github.Anuken.Arc'){
            details.useVersion "$mindustryVersion"
        }
    }
}


base {
    archivesName = "Agzam-s_plugin.jar"
    distsDirectory = layout.buildDirectory.dir('custom-dist')
    libsDirectory = layout.buildDirectory.dir('libs')
}


jar{
    baseName "${project.name}"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE 
    
    from{
        configurations.runtimeClasspath.collect{it.isDirectory() ? it : zipTree(it)}
    }

    from(rootDir){
        include "plugin.json"
        include "assets/colors.png"
        include "assets/bundles/bundle.properties"
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.java
            }
        }
    }
}